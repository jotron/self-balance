## MPU-6050

Als aller erstes haben wir den Sensor mit dem Arduino verbunden und die rohen Daten abgelesen. 

![Fig (4) : CONNECTION DIAGRAM (tutorial 1)](https://hackster.imgix.net/uploads/image/file/125190/conn.png?auto=compress%2Cformat&w=680&h=510&fit=max) 

```c
int16_t ax, ay, az;
int16_t gx, gy, gz;

void setup() {
    // join I2C bus (I2Cdev library doesn't do this automatically)
    #if I2CDEV_IMPLEMENTATION == I2CDEV_ARDUINO_WIRE
        Wire.begin();
    #elif I2CDEV_IMPLEMENTATION == I2CDEV_BUILTIN_FASTWIRE
        Fastwire::setup(400, true);
    #endif

    // initialize serial communication
    // (38400 chosen because it works as well at 8MHz as it does at 16MHz, but
    // it's really up to you depending on your project)
    Serial.begin(9600);

    // initialize device
    Serial.println("Initializing I2C devices...");
    accelgyro.initialize();

    // verify connection
    Serial.println("Testing device connections...");
    Serial.println(accelgyro.testConnection() ? "MPU6050 connection successful" : "MPU6050 	   connection failed");

    accelgyro.setXGyroOffset(220);
    accelgyro.setYGyroOffset(76);
    accelgyro.setZGyroOffset(-85);
}

void loop() {
	accelgyro.getMotion6(&ax, &ay, &az, &gx, &gy, &gz);
}
```



Dabei haben uns die [offzielle Arduino Dokumentation](https://playground.arduino.cc/Main/MPU-6050), [ein Tutorial](https://create.arduino.cc/projecthub/Aritro/getting-started-with-imu-6-dof-motion-sensor-96e066) sowie die Beispiele in der Bibiliothek geholfen. Den vollständigen, funktionierenden Beispiel-Code findet man [hier](https://raw.githubusercontent.com/jrowberg/i2cdevlib/master/Arduino/MPU6050/examples/MPU6050_raw/MPU6050_raw.ino).

Um aus den rohen Daten die Neigung zu bekommenhaben wir anfänglich die rohen Accelerometer und Gyroskop-Werte mittels [Komplementären-Filter](https://bayesianadventures.wordpress.com/2013/10/20/gyroscopes-accelerometers-and-the-complementary-filter/) kombiniert um den Winkel zur Senkrechtachse zu bekommen. 

```c
void loop() {
    // read raw accel/gyro measurements from device
    accelgyro.getMotion6(&ax, &ay, &az, &gx, &gy, &gz);

    // Convert accelerometer raw values to degrees
    accXangle = (atan2(ay, az) + PI) * RAD_TO_DEG;
    accYangle = (atan2(ax, az) + PI) * RAD_TO_DEG;

    //Gyroskop in grad/sec umwandeln
    gx_correct = gx / 131;
    gy_correct = gy / 131;
    gz_correct = gz / 131;

    // Komplämentärer Filter
    compAngleX = (0.98 * (compAngleX + (gx_correct * (double)(micros() - timer) / 			1000000))) + (0.02 * accXangle);
    delta = 180-compAngleX;

    timer = micros();
}
```

Das hat zwar funktioniert war aber rechenaufwendig. Deshalb haben wir uns schlussendlich doch auf den *Digital Motion Processor* des Sensor's verlassen. Der Sensor kann nämlich die Daten selber schon auswerten und die Neigung zurückgeben. ([hilfreiches Beispiel-Programm](https://github.com/jrowberg/i2cdevlib/blob/master/Arduino/MPU6050/examples/MPU6050_DMP6/MPU6050_DMP6.ino)).
Diese Methode ist allerdings komplizierter da der Buffer des Chips direkt abgelesen werden muss.

```c
void setup() {
    // join I2C bus (I2Cdev library doesn't do this automatically)
    #if I2CDEV_IMPLEMENTATION == I2CDEV_ARDUINO_WIRE
        Wire.begin();
        Wire.setClock(400000); // 400kHz I2C clock. Comment this line if having 			compilation difficulties
    #elif I2CDEV_IMPLEMENTATION == I2CDEV_BUILTIN_FASTWIRE
        Fastwire::setup(400, true);
    #endif

    // initialize serial communication
    Serial.begin(9600);
  
    // initialize device
    Serial.println(F("Initializing I2C devices..."));
    mpu.initialize();
    pinMode(INTERRUPT_PIN, INPUT);

    // verify connection
    Serial.println(mpu.testConnection() ? F("MPU6050 connection successful") : F("MPU6050 	  connection failed"));

    // load and configure the DMP
    devStatus = mpu.dmpInitialize();

    // supply your own gyro offsets here, scaled for min sensitivity
    mpu.setXGyroOffset(220);
    mpu.setYGyroOffset(76);
    mpu.setZGyroOffset(-85);
    mpu.setZAccelOffset(1788); // 1688 factory default for my test chip

    // make sure it worked (returns 0 if so)
    if (devStatus == 0) {
        // turn on the DMP, now that it's ready
        Serial.println(F("Enabling DMP..."));
        mpu.setDMPEnabled(true);

        // enable Arduino interrupt detection
        Serial.println(F("Enabling interrupt detection (Arduino external interrupt 				0)..."));
        attachInterrupt(digitalPinToInterrupt(INTERRUPT_PIN), dmpDataReady, RISING);
        mpuIntStatus = mpu.getIntStatus();

        // set our DMP Ready flag so the main loop() function knows it's okay to use it
        Serial.println(F("DMP ready! Waiting for first interrupt..."));
        dmpReady = true;

        // get expected DMP packet size for later comparison
        packetSize = mpu.dmpGetFIFOPacketSize();
    } else {
        // ERROR!
        // 1 = initial memory load failed
        // 2 = DMP configuration updates failed
        // (if it's going to break, usually the code will be 1)
        Serial.print(F("DMP Initialization failed (code "));
        Serial.print(devStatus);
        Serial.println(F(")"));
    }
}

void loop() {
    // if programming failed, don't try to do anything
    if (!dmpReady) return;

    // wait for MPU interrupt or extra packet(s) available
    while (!mpuInterrupt && fifoCount < packetSize) {
        // other program behavior stuff here
    }

    // reset interrupt flag and get INT_STATUS byte
    mpuInterrupt = false;
    mpuIntStatus = mpu.getIntStatus();

    // get current FIFO count
    fifoCount = mpu.getFIFOCount();

    // check for overflow (this should never happen unless our code is too inefficient)
    if ((mpuIntStatus & 0x10) || fifoCount == 1024) {
        // reset so we can continue cleanly
        mpu.resetFIFO();
        Serial.println(F("FIFO overflow!"));

    // otherwise, check for DMP data ready interrupt (this should happen frequently)
    } else if (mpuIntStatus & 0x02) {
        // wait for correct available data length, should be a VERY short wait
        while (fifoCount < packetSize) fifoCount = mpu.getFIFOCount();

        // read a packet from FIFO
        mpu.getFIFOBytes(fifoBuffer, packetSize);
        
        // track FIFO count here in case there is > 1 packet available
        // (this lets us immediately read more without waiting for an interrupt)
        fifoCount -= packetSize;

        mpu.dmpGetQuaternion(&q, fifoBuffer);
        mpu.dmpGetGravity(&gravity, &q);
        mpu.dmpGetYawPitchRoll(ypr, &q, &gravity);
        Serial.print("ypr\t");
        Serial.print(ypr[0] * 180/M_PI);
        Serial.print("\t");
        Serial.print(ypr[1] * 180/M_PI);
        Serial.print("\t");
        Serial.println(ypr[2] * 180/M_PI);
    }
}
```



Um die Präzision zu erhöhen mussten wir die Sensor-spezifische Fehlabneigungen (offset) berechnen. Dazu geholfen hat uns das [Skript](http://wired.chillibasket.com/2015/01/calibrating-mpu6050/) von *Luis Ródenas*.